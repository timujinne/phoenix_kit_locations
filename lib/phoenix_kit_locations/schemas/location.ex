defmodule PhoenixKitLocations.Schemas.Location do
  @moduledoc "Schema for locations."

  use Ecto.Schema
  use PhoenixKit.SchemaPrefix
  import Ecto.Changeset

  @type t :: %__MODULE__{}

  @primary_key {:uuid, UUIDv7, autogenerate: true}
  @foreign_key_type UUIDv7

  @statuses ~w(active inactive)

  schema "phoenix_kit_locations" do
    field(:name, :string)
    field(:description, :string)
    field(:public_notes, :string)

    # Address fields (international standard)
    field(:address_line_1, :string)
    field(:address_line_2, :string)
    field(:city, :string)
    field(:state, :string)
    field(:postal_code, :string)
    field(:country, :string)

    # Contact
    field(:phone, :string)
    field(:email, :string)
    field(:website, :string)

    # Internal
    field(:notes, :string)
    field(:status, :string, default: "active")

    # Owner (a `phoenix_kit_users` uuid — a person or an organization
    # account). `nil` is a global location. Deliberately NOT in the
    # `changeset/2` cast list: only `owner_changeset/2` sets it.
    field(:owner_uuid, UUIDv7)

    # Features (wheelchair_accessible, elevator, parking, etc.)
    field(:features, :map, default: %{})

    # Multilang translations
    field(:data, :map, default: %{})

    has_many(:location_type_assignments, PhoenixKitLocations.Schemas.LocationTypeAssignment,
      foreign_key: :location_uuid,
      references: :uuid
    )

    has_many(:location_types, through: [:location_type_assignments, :location_type])

    timestamps(type: :utc_datetime)
  end

  @required_fields [:name]
  @optional_fields [
    :description,
    :public_notes,
    :address_line_1,
    :address_line_2,
    :city,
    :state,
    :postal_code,
    :country,
    :phone,
    :email,
    :website,
    :notes,
    :status,
    :features,
    :data
  ]

  def changeset(location, attrs) do
    location
    |> cast(attrs, @required_fields ++ @optional_fields)
    |> validate_required(@required_fields)
    |> validate_length(:name, min: 1, max: 255)
    |> validate_length(:description, max: 2000)
    |> validate_length(:public_notes, max: 2000)
    |> validate_length(:notes, max: 5000)
    |> validate_length(:address_line_1, max: 500)
    |> validate_length(:address_line_2, max: 500)
    |> validate_length(:city, max: 255)
    |> validate_length(:state, max: 255)
    |> validate_length(:postal_code, max: 20)
    |> validate_length(:country, max: 255)
    |> validate_length(:phone, max: 50)
    |> validate_length(:email, max: 255)
    |> validate_length(:website, max: 500)
    |> validate_inclusion(:status, @statuses)
    |> maybe_validate_email()
    |> maybe_validate_website()
  end

  @doc """
  Changeset for a location's owner, kept apart from `changeset/2` so the
  general create/update path (and any form that forwards browser params into
  it) can never set it. Accepts a `Location` or an existing changeset;
  `owner_uuid` is a `phoenix_kit_users` uuid or `nil`. An unknown user comes
  back from the repo as an `:owner_uuid` error rather than a raise.
  """
  @spec owner_changeset(t() | Ecto.Changeset.t(), String.t() | nil) :: Ecto.Changeset.t()
  def owner_changeset(location_or_changeset, owner_uuid) do
    location_or_changeset
    |> cast(%{owner_uuid: owner_uuid}, [:owner_uuid])
    |> foreign_key_constraint(:owner_uuid)
  end

  defp maybe_validate_email(changeset) do
    case get_field(changeset, :email) do
      nil -> changeset
      "" -> changeset
      _ -> validate_format(changeset, :email, ~r/@/, message: "must be a valid email address")
    end
  end

  defp maybe_validate_website(changeset) do
    case get_field(changeset, :website) do
      nil ->
        changeset

      "" ->
        changeset

      _ ->
        validate_format(changeset, :website, ~r/^https?:\/\//,
          message: "must start with http:// or https://"
        )
    end
  end
end
